(*
	SCRIPT NAME:	Copy EN Note Link (Classic)
	
	DATE:	Sat, Jul 19, 2014		VER: 1.0
	
	PURPOSE:
		•  Get Classic link of selected Evernote Note
		•  Copy to Mac Clipboard as RTF (Rich Text Format) so that you can
			paste to most apps as a click-able link
		•  Text is EN Note Title
		•  Link is the "Classic", internal link to EN Mac Note
		
	AUTHOR:		JMichael (member of Discussion.Evernote.com forum)
				Please PM me with any bugs/issues/questions
				
	INSTALLATION:
		•  There are several ways you can install and access this script
		•  SIMPLE METHOD
			•  Copy to your personal Scripts folder:  /Users/<YourName>/Library/Scripts/
				(create the Scripts folder if it does not exist)
			•  Select a Note in Evernote
			•  Click on the Apple Scripts menu in the upper right area of your menu
			•  Click on EN Note Link (classic)
			•  Switch to your target app, and click in the text area where you want the link, and press CMD-V
		•  OTHER OPTIONS
			•  FastScripts -- http://www.red-sweater.com/fastscripts/
			•  Automator
			•  See article on Veritrope.com for details:  http://veritrope.com/tech/the-basics-using-keyboard-shortcuts-with-applescripts/
	
	REF:		The below people really wrote all the parts to this script.
			I just found them and put them together in this script
			
			1. From this thread on Discussion.Evernote.com forum:
				https://discussion.evernote.com/topic/60623-how-can-make-the-classic-link-the-default-way-of-linking/
				
				• Post by patnpm
					https://discussion.evernote.com/topic/60623-how-can-make-the-classic-link-the-default-way-of-linking/#entry283106
				• Post by DanielB
					https://discussion.evernote.com/topic/60623-how-can-make-the-classic-link-the-default-way-of-linking/#entry291008
					
			2. From this thread on MacScripter.net forum
				http://macscripter.net/viewtopic.php?pid=102416
				
				• Post by soboks, 2008-07-24
					http://macscripter.net/viewtopic.php?pid=102416#p102416
						
				• Post by regulus6633, 2008-07-24
					http://macscripter.net/viewtopic.php?pid=102418#p102418
*)

tell application "Evernote"
	
	set _sel to selection -- Gets the Note(s) Selected in Evernote
	if _sel ≠ {} then
		set aNote to first item of _sel -- Get ONLY the 1st Note
		set noteTitle to (get title of aNote)
		set noteLink to (get note link of aNote)
		my copyLinkToClipboardAsRTF(noteLink, noteTitle)
		
	end if
end tell

--=====================================
--	SUBPROGRAMS
--=====================================

on copyLinkToClipboardAsRTF(pURL, plinkText)
	
	
	(*
	------------------------------------------------------------------------------
	PURPOSE:  Creates click-able HTML Link and Copies to Clipboard as RTF
				A normal user paste should work into most, if not all, apps that support RTF
				
	DATE:	Sat, Jul 19, 2014		VER: 1.0
	
	COMMENTS:
		•  Can't use the AppleScript "set the clipboard to" command because it will just copy the link as plain text
		•  I don't understand the data in the "set cmd to" line, but it seems to work
		• The HTML version of this is:  <a href="url">Link text</a>
	REF:  
		• soboks post on MacScripter.net forum, 2008-07-24
			http://macscripter.net/viewtopic.php?pid=102416#p102416
	-------------------------------------------------------------------------------
	*)
	
	set cmd to "\"{\\rtf1\\ansi\\ansicpg1252\\cocoartf949\\cocoasubrtf270{\\fonttbl\\f0\\fswiss\\fcharset0 Helvetica;}{\\colortbl;\\red255\\green255\\blue255;}\\margl1440\\margr1440\\vieww9000\\viewh8400\\viewkind0\\pard\\tx720\\tx1440\\tx2160\\tx2880\\tx3600\\tx4320\\tx5040\\tx5760\\tx6480\\tx7200\\tx7920\\tx8640\\ql\\qnatural\\pardirnatural{\\field{\\*\\fldinst{HYPERLINK \"" & pURL & "\"}}{\\fldrslt\\f0\\fs24 \\cf0 \"" & plinkText & "\"}}}\""
	
	do shell script "/bin/bash -c 'echo " & cmd & " | pbcopy -Prefer rtf'"
	
	return
	
end copyLinkToClipboardAsRTF
